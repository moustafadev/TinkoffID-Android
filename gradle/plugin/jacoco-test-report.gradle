apply from: "$project.rootDir/gradle/plugin/jacoco-common.gradle"

afterEvaluate { project ->
    if (isAndroidModule(project)) setupAndroidReporting()
    else setupKotlinReporting()
}

def setupKotlinReporting() {
    jacocoTestReport {
        dependsOn test
        reports {
            csv.required = false
            xml.destination = file("${buildDir}/reports/jacoco/jacoco.xml")
            html.destination = file("${buildDir}/reports/jacoco")
        }
    }
}

def setupAndroidReporting() {
    task jacocoTestReport(
            type: JacocoReport,
            dependsOn: ['testDebugUnitTest']
    ) {
        reports {
            csv.required = false
            xml.destination = file("${buildDir}/reports/jacoco/jacoco.xml")
            html.destination = file("${buildDir}/reports/jacoco")
        }
        def debugTree = fileTree(
                dir: "$buildDir/tmp/kotlin-classes/debug",
                excludes: fileFilter
        )
        def mainSrc = "$projectDir/src/main/java"
        sourceDirectories.from = files([mainSrc])
        classDirectories.from = files([debugTree])
        executionData.from = fileTree(
                dir: project.buildDir,
                includes: [
                        'jacoco/testDebugUnitTest.exec',
                        'outputs/code-coverage/connected/*coverage.ec'
                ]
        )
    }
}
